import com.aouerfelli.subwatcher.Versions

buildscript {
  repositories {
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${Versions.androidGradlePlugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
    classpath "org.jlleitschuh.gradle:ktlint-gradle:${Versions.ktlintGradle}"
    classpath "com.squareup.sqldelight:gradle-plugin:${Versions.sqlDelight}"
    classpath "com.google.dagger:hilt-android-gradle-plugin:${Versions.dagger}-alpha"
    classpath "com.github.ben-manes:gradle-versions-plugin:${Versions.gradleVersions}"
  }
}

apply plugin: "com.github.ben-manes.versions"

allprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"

  ktlint {
    // Update version manually to fix some false positives
    version = "0.40.0"
  }

  repositories {
    google()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  }

  // TODO: Also do this globally for Android
  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      allWarningsAsErrors = true
      useIR = true
      freeCompilerArgs += [
          "-progressive",
          "-Xinline-classes",
          "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
          "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
          "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
      ]
    }
  }
}
