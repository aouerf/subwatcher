plugins {
  id "com.android.application"
  id "com.squareup.sqldelight"
  id "dagger.hilt.android.plugin"
  id "org.jetbrains.kotlin.android"
  id "org.jetbrains.kotlin.kapt"
  id "org.jetbrains.kotlin.plugin.serialization"
}

android {
  compileSdkVersion 31

  defaultConfig {
    applicationId "com.aouerfelli.subwatcher"
    minSdkVersion 21
    targetSdkVersion 31
    versionCode 5
    versionName "1.0.4"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles "proguard-rules.pro"
    }
  }

  lintOptions {
    checkReleaseBuilds false
    textReport true
    htmlReport false
    xmlReport false
    explainIssues false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }

  kotlinOptions {
    allWarningsAsErrors = true
    jvmTarget = "1.8"
    freeCompilerArgs += [
        "-progressive",
        "-opt-in=kotlin.RequiresOptIn"
    ]
  }

  kapt {
    correctErrorTypes true
  }

  buildFeatures {
    viewBinding true
  }
}

sqldelight {
  Database {
    packageName = "com.aouerfelli.subwatcher.database"
  }
}

dependencies {
  coreLibraryDesugaring libs.android.desugarJdkLibs

  implementation libs.bundles.androidx

  implementation libs.coroutines.android

  implementation libs.serialization

  implementation libs.insetter
  implementation libs.coil

  implementation libs.bundles.daggerAndroidRuntime
  kapt libs.bundles.daggerAndroidCompiler

  implementation libs.okhttp.client
  implementation libs.okhttp.loggingInterceptor
  implementation libs.bundles.retrofit

  implementation libs.sqldelight.android
  implementation libs.sqldelight.coroutines

  implementation libs.timber

  testImplementation libs.junit
  testImplementation libs.mockk
  testImplementation libs.coroutines.test
  testImplementation libs.okhttp.mockWebServer
  testImplementation libs.sqldelight.jvm
  kaptTest libs.dagger.compiler
}
