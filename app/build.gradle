plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "org.jetbrains.kotlin.kapt"
  id "com.squareup.sqldelight"
  id "dagger.hilt.android.plugin"
}

android {
  compileSdkVersion 30

  defaultConfig {
    applicationId "com.aouerfelli.subwatcher"
    minSdkVersion 21
    targetSdkVersion 30
    versionCode 5
    versionName "1.0.4"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles "proguard-rules.pro"
    }
  }

  lintOptions {
    checkReleaseBuilds false
    textReport true
    htmlReport false
    xmlReport false
    explainIssues false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }

  kotlinOptions {
    allWarningsAsErrors = true
    jvmTarget = "1.8"
    freeCompilerArgs += [
        "-progressive",
        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
        "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
        "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
    ]
  }

  kapt {
    correctErrorTypes true
  }

  buildFeatures {
    viewBinding true
  }
}

sqldelight {
  Database {
    packageName = "com.aouerfelli.subwatcher.database"
  }
}

dependencies {
  coreLibraryDesugaring libs.android.desugarJdkLibs

  implementation libs.bundles.androidx

  implementation libs.coroutines.android

  implementation libs.insetter
  implementation libs.coil

  implementation libs.bundles.daggerAndroidRuntime
  kapt libs.bundles.daggerAndroidCompiler

  implementation libs.okhttp.client
  implementation libs.okhttp.loggingInterceptor
  implementation libs.bundles.retrofit
  implementation libs.moshi.runtime
  kapt libs.moshi.compiler

  implementation libs.sqldelight.android
  implementation libs.sqldelight.coroutines

  implementation libs.timber

  testImplementation libs.junit
  testImplementation libs.mockk
  testImplementation libs.coroutines.test
  testImplementation libs.okhttp.mockWebServer
  testImplementation libs.sqldelight.jvm
  kaptTest libs.dagger.compiler
}
